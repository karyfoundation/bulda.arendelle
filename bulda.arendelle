
// Copyright 2014 Pouya Kary
// Bulda was the first version
// of the language with only 5
// commands for navigating and
// painting that I wrote in my 
// CASIO Prizm (fx-CG10)
// calculator for fun.
      
// Also it's good to know that
// Bulda was the troll who kept
// critoff and sven in Disney's
// full featured animation movie
// Frozen!
   
// title
   'The Bulda Language'

// doing for ever
   ( WhileSign , 1 )
   [ @WhileSign = 1 ,
   
      // asking for the code:
         ( code , "---- Î» ---- ")
      
      // calculating the size
         ( size , Floor ( Log10 ( @code ) ) + 1 )
      
      // controling sp
         ( i , 1 )
         ( x , 0 )
         ( y , 0 )
      
      
         //////////////////////////////
         /// MAIN BULDA INTERPRETER ///
         //////////////////////////////
      
      // main while
   
         { @code = 0 , ( size , 1 ) }
   
         [ @size ,
               
            // reading the command
               ( command , 
                   
                   Floor ( @code / Pow ( 10 , @i - 1 ) )
                   - Floor ( @code / Pow ( 10 , @i ) ) * 10
              
               )
      
            // up command
               { @command = 1 ,
                  ( y , -1 )
               , 
               // right commnad
                  { @command = 2 ,
                     ( x , +1 )
                  ,
                  // left commnad
                     { @command = 3 ,
                        ( x , -1 )
                     ,
                     // down command
                        { @command = 4 ,
                           ( y , +1 )
                        ,        
                        // paint command
                           { @command = 5 ,
                                    
                              i [ @x * 2 , r ]
                              [ @y , d ] prp
                           ,
                           // exit command
                              { @command = 6 ,
                            	 ( WhileSign , 0 )     
                              }
                           }                   
                        }
                     }
                  }
               }
   
            // going to the next char
               ( i , +1 )
   
         ]
   
      // waiting to see the result
         s
   
   ]
   
// done
